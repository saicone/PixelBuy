plugins {
    id 'java'
    id 'idea'
    id 'com.gradleup.shadow' version '9.2.2'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://central.sonatype.com/repository/maven-snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    maven { url 'https://maven.devs.beer/' }
    maven { url 'https://repo.bg-software.com/repository/api/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT'

    compileOnly project(':fakeapi')
    compileOnly 'com.github.oraxen:oraxen:1.173.0'
    compileOnly('io.lumine:MythicLib-dist:1.7.1-SNAPSHOT') { transitive = false } // Required by MMOItems
    compileOnly('net.Indyuce:MMOItems-API:6.10.1-SNAPSHOT') { transitive = false }
    compileOnly('dev.lone:api-itemsadder:4.0.10') { transitive = false }
    compileOnly 'com.bgsoftware:WildToolsAPI:2025.2'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.luckperms:api:5.4'

    // Library loader
    implementation 'com.saicone.ezlib:ezlib:1.3.2'
    implementation 'com.saicone.ezlib:loader:1.3.2'
    compileOnly 'com.saicone.ezlib:annotations:1.3.2'
    annotationProcessor 'com.saicone.ezlib:annotations:1.3.2'

    // Implementation
    compileOnly 'com.github.cryptomorin:XSeries:13.5.1'
    compileOnly 'com.saicone.rtag:rtag:1.5.13'
    compileOnly 'com.saicone.rtag:rtag-item:1.5.13'
    compileOnly 'com.saicone.nbt:nbt:1.0.2'
    compileOnly 'com.saicone.delivery4j:delivery4j:1.1.4'
    compileOnly 'com.saicone.delivery4j:broker-sql-hikari:1.1.4'
    compileOnly 'com.saicone.delivery4j:broker-redis:1.1.4'
    compileOnly 'com.saicone.delivery4j:broker-rabbitmq:1.1.4'

    compileOnly 'org.jetbrains:annotations:26.0.2'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    // Hook
    // > Nexo - Not compatible with Java 11
    relocate 'fakeapi.FakeApi1', 'com.nexomc.nexo.api.NexoItems'
    relocate 'fakeapi.FakeItem1', 'com.nexomc.nexo.items.ItemBuilder'
    // Libraries
    relocate 'com.saicone.ezlib', project.group + '.ezlib'
    relocate 'com.cryptomorin.xseries', project.group + '.libs.xseries'
    relocate 'com.saicone.rtag', project.group + '.libs.rtag'
    relocate 'com.zaxxer.hikari', project.group + '.libs.hikari'
    relocate 'redis.clients.jedis', project.group + '.libs.jedis'
    relocate 'com.rabbitmq', project.group + '.libs.rabbitmq'
    relocate 'com.google.common', project.group + '.libs.guava'
    relocate 'com.google.gson', project.group + '.libs.gson'
    relocate 'com.saicone.delivery4j', project.group + '.libs.delivery4j'
    relocate 'com.saicone.nbt', project.group + '.libs.nbt'
    minimize()
}