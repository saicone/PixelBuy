plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
    mavenCentral()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'

    compileOnly 'com.github.Th0rgal:Oraxen:1.162.0'
    compileOnly('net.Indyuce:MMOItems:6.7.3') { transitive = false }

    implementation 'com.squareup.okhttp3:okhttp:3.14.6'
    implementation 'com.squareup.okio:okio:1.17.2'

    compileOnly 'org.jetbrains:annotations:24.0.1'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'okhttp3', project.group + '.lib.okhttp3'
    relocate 'okio', project.group + '.lib.okio'
    minimize()
}