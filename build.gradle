plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://nexus.phoenixdevt.fr/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    mavenCentral()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'

    compileOnly 'com.github.oraxen:oraxen:1.162.0'
    compileOnly('net.Indyuce:MMOItems-API:6.9.2-SNAPSHOT') { transitive = false }
    compileOnly 'me.clip:placeholderapi:2.11.3'

    implementation 'com.github.cryptomorin:XSeries:9.6.1.1'
    implementation 'com.saicone.rtag:rtag:1.4.1'
    implementation 'com.saicone.rtag:rtag-item:1.4.1'
    implementation 'com.zaxxer:HikariCP:5.0.1'

    compileOnly 'org.jetbrains:annotations:24.0.1'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.cryptomorin.xseries', project.group + '.libs.xseries'
    relocate 'com.saicone.rtag', project.group + '.libs.rtag'
    relocate 'com.zaxxer.hikari', project.group + '.libs.hikari'
    minimize()
}